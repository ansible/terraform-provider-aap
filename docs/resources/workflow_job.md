---
page_title: "aap_workflow_job Resource - terraform-provider-aap"
description: |-
  Launches an AAP workflow job.
  A workflow job is launched only when the resource is first created or when the resource is changed. The triggers argument can be used to launch a new workflow job based on any arbitrary value.
  This resource always creates a new workflow job in AAP. A destroy will not delete a workflow job created by this resource, it will only remove the resource from the state.
---

# aap_workflow_job (Resource)

Launches an AAP workflow job.

A workflow job is launched only when the resource is first created or when the resource is changed. The `triggers` argument can be used to launch a new workflow job based on any arbitrary value.

This resource always creates a new workflow job in AAP. A destroy will not delete a workflow job created by this resource, it will only remove the resource from the state.

-> **Note** To pass an inventory to an aap_workflow_job resource, the underlying workflow job template *must* have been configured to prompt for the inventory on launch.

!> **Warning** If an AAP Workflow Job launched by this resource is deleted from AAP, the resource will be removed from the state and a new workflow job will be created to replace it.


## Example Usage

```terraform
terraform {
  required_providers {
    aap = {
      source = "ansible/aap"
    }
  }
}

provider "aap" {
  host     = "https://AAP_HOST"
  username = "ansible"
  password = "test123!"
}

resource "aap_inventory" "my_inventory" {
  name         = "A new inventory"
  organization = 1
}

resource "aap_workflow_job" "sample_foo" {
  workflow_job_template_id = 8
  inventory_id             = aap_inventory.my_inventory.id
}

locals {
  values_extra_vars = <<EOT
exampleVariables:
  - name: "bar"
    namespace: "bar-namespace"
    type: 0
EOT
}

resource "aap_workflow_job" "sample_bar" {
  workflow_job_template_id = 8
  extra_vars               = jsonencode(yamldecode(local.values_extra_vars))
}

resource "aap_workflow_job" "sample_baz" {
  workflow_job_template_id = 8
  extra_vars = jsonencode({
    execution_environment_id = "3"
    # Add other variables as needed
  })
}

resource "aap_workflow_job" "sample_abc" {
  workflow_job_template_id = 8
  inventory_id             = aap_inventory.my_inventory.id
  extra_vars               = yamlencode({ "os" : "Linux", "automation" : "ansible" })
}

resource "aap_workflow_job" "sample_xyz" {
  workflow_job_template_id = 8
  inventory_id             = aap_inventory.my_inventory.id
  extra_vars               = "os: Linux\nautomation: ansible-devel"
}

output "job_foo" {
  value = aap_workflow_job.sample_foo
}

output "job_bar" {
  value = aap_workflow_job.sample_bar
}

output "job_baz" {
  value = aap_workflow_job.sample_baz
}

output "job_abc" {
  value = aap_workflow_job.sample_abc
}

output "job_xyz" {
  value = aap_workflow_job.sample_xyz
}
```


## Ensuring Workflow Jobs Launch on Hosts created and Inventories updated in the same configuration

### Advanced Usage - `depends_on` in `aap_workflow_job` `resource` for `aap_host` `resource` creation
-> **Note** if you have HCL that creates an `aap_host` `resource` in an already existing `aap_inventory`, you will have to add a `depends_on` clause in the `aap_workflow_job` `resource` block of the `aap_workflow_job` that needs that `aap_host` to exist in the `aap_inventory` used for the `aap_workflow_job` creation.

If you do not use the depends_on clause as illustrated below you may run into a race condition where the workflow job will attempt to launch before the inventory is updated with the host required.

### Example HCL for this scenario:

```terraform
data "aap_inventory" "inventory" {
  name              = "Demo Inventory"
  organization_name = "Default"
}

resource "aap_host" "host" {
  inventory_id = data.aap_inventory.inventory.id
  name         = "127.0.0.1"
}

data "aap_workflow_job_template" "workflow_job_template" {
  name              = "Demo Workflow Job Template"
  organization_name = "Default"
}

resource "aap_workflow_job" "workflow_job" {
  workflow_job_template_id = data.aap_workflow_job_template.workflow_job_template.id
  inventory_id             = data.aap_inventory.inventory.id

  # Force creation of this resource to wait for the aap_host.host resource to be created
  depends_on = [
    aap_host.host
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `workflow_job_template_id` (Number) Id of the workflow job template.

### Optional

- `extra_vars` (String) Extra Variables. Must be provided as either a JSON or YAML string.
- `inventory_id` (Number) Identifier for the inventory the job will be run against.
- `triggers` (Map of String) Map of arbitrary keys and values that, when changed, will trigger a creation of a new Workflow Job on AAP. Use 'terraform taint' if you want to force the creation of a new workflow job without changing this value.

### Read-Only

- `ignored_fields` (List of String) The list of properties set by the user but ignored on server side.
- `job_type` (String) Job type
- `status` (String) Status of the workflow job
- `url` (String) URL of the workflow job template
